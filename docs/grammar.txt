program       = object-block, tool-block, process-block. 

(* ---- определения блока объектов ---- *)

object-block  = "объекты", ":", object-list, ";".

object-list   = {object-decl}.

object-decl   = object-id, ";".

object-id     = id.

(* ---- определения блока инструментов ---- *)

tool-block    = "инструменты", ":", tool-list, ";".

tool-list     = {tool-decl}.

tool-decl     = tool-id, ":", tool, ";".

tool-id       = "*", id.

tool          = type, named-params.

type          = coords
              | "прямая"
              | "дуга"
              | "зона"
              | "линия"
              | "счетчик".

(* ---- определения блока обработки ---- *)

process-block = "обработка", ":", process-list, ";".

process-list  = {(process-decl | condition)}.

process-decl  = process-id, ":", process-body, ";".

process-id    = "_", id.

process-body  = action 
              | event-disj.

action        = [action-name, "(", params, ")"].

action-name   = "оповестить"
              | "сохранить"
              | "увеличить"
              | "уменьшить"
              | "обнулить".

event-disj    = [event-disj, "или",] event-conj.

event-conj    = [event-conj, "и",] event-or-id.

event-or-id   = event
              | "(", event-disj, ")"
              | process-id.

event         = (object-id | tool-id), ".", event-name, "(", params, ")".

event-name    = "пересекает"
              | "входитВ"
              | "покидает"
              | "отклоняетсяОт"
              | "ускоряется"
              | "замедляется"
              | "неподвижен"
              | "появляется"
              | "исчезает"
              | "равен"
              | "превышает"
              | "ниже".

condition     = "если", event-disj, ":", {(process-id | action), ";"}, ";".

(* ---- вспомогательные определения ---- *)

id            = (letter | latin), {letter | latin | "_" | number}.

letter        = "А" | "Б" | "В" | "Г" | "Д" | "Е" 
              | "Ё" | "Ж" | "З" | "И" | "Й" | "К" 
              | "Л" | "М" | "Н" | "О" | "П" | "Р"
              | "С" | "Т" | "У" | "Ф" | "Х" | "Ц" 
              | "Ч" | "Ш" | "Щ" | "Ъ" | "Ы" | "Ь" 
              | "Э" | "Ю" | "Я" | "а" | "б" | "в" 
              | "г" | "д" | "е" | "ё" | "ж" | "з" 
              | "и" | "й" | "к" | "л" | "м" | "н" 
              | "о" | "п" | "р" | "с" | "т" | "у" 
              | "ф" | "х" | "ц" | "ч" | "ш" | "щ" 
              | "ъ" | "ы" | "ь" | "э" | "ю" | "я". 

latin         = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I"
              | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R"
              | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a"
              | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
              | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s"
              | "t" | "u" | "v" | "w" | "x" | "y" | "z".

number        = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9".

int           = ["-"], number.

string        = '"', {character}, '"'.

(* множество значений нетерминала character представляет собой множество любых возможных символов, включая множества значений нетерминалов letter, latin и number. *)

coords        = "(", int, ",", int, ")".

colour        = "rgb", "(", int, ",", int, ",", int, ")".

value         = int
              | string
              | coords
              | colour
              | object-id
              | tool-id
              | tool-parts
              | side-disj.

side-disj     = [side-disj, "или", ] side-conj.              

side-conj     = [side-conj, "и", ] side.

side          = "слева"
              | "сверху"              
              | "справа"              
              | "снизу"
              | "(", side-disj, ")".

params        = [value, {",", value}].

named-params  = {",", param-name, "=", value}.

param-name    = ""
              | ""
              | "".

comment-line  = 2 * "-", {character – "\n"}, ("\n" | EOT).

(* EOT – фиктивный терминал, обозначающий конец текста. *)
